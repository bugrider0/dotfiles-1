#!/usr/bin/env zsh

# {{{
typeset -Ag snippets

snippet-add() {
    snippets[$1]="$2"
}

snippet-expand() {
    emulate -L zsh
    setopt extendedglob
    local MATCH

    LBUFFER=${LBUFFER%%(#m)[.\-+:|_a-zA-Z0-9]#}
    LBUFFER+=${snippets[$MATCH]:-$MATCH}
}
zle -N snippet-expand

bindkey '^\' snippet-expand
# }}}

snippet-add f                   "| fzf "
snippet-add p                   "| pager "
snippet-add wallpaper           "feh --bg-fill "
snippet-add pb                  "| px pastebinit | xclip -selection clipboard && xclip -o -selection clipboard"
snippet-add colors              "| grep -Eo '#[[:alpha:][:digit:]]{6,8}' | cut -b 1-7 | sort -u > colors"
snippet-add kwin-shortcuts      "qdbus org.kde.kglobalaccel /component/kwin org.kde.kglobalaccel.Component.shortcutNames | fzf"
snippet-add xorg-class          "xprop WM_CLASS"
